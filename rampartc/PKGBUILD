# Contributor: Tom Tryfonidis <tomtryf@gmail.com>
# Contributor: Peter Mylemans <peter.mylemans@gmail.com>
# Contributor: Tom Tryfonidis <tomtryf@gmail.com>
#
# This is a modified version for eucalyptus cloud

pkgname=rampartc
pkgver=1.3.0
pkgrel=1
pkgdesc="Apache Rampart/C is the security module for Apache Axis2/C."
arch=('i686' 'x86_64')
url="http://axis.apache.org/axis2/c/rampart/"
license=('Apache')
depends=("axis2c>=1.6")
source=(http://archive.apache.org/dist/ws/rampart/c/1_3_0/$pkgname-src-$pkgver.tar.gz
rampartc-1.3.0-destdir.patch
rampartc-1.3.0-quoting.patch
rampartc-1.3.0-cast.patch
rampartc-1.3.0-segfault.patch
rampartc-1.3.0-memleak.patch
rampartc-1.3.0-timestamp.patch
rampartc-1.3.0-c14n.patch
make_paths.patch)
md5sums=('3420fd8cdd4c70e6f18c27c94ba86384'
         'd90789b21664946fa4036ca397dcd1ef'
         '24abeb6ced20249e8e9adbb60d2ce327'
         'f9d3b5bbbfd52c86b9d58d29c647a7c6'
         '8d3072e4da799767a097334d9df6d11e'
         '3c27d19504f7a52dd694de7ea147b30d'
         '27384263f1244ea62f0573ea2696fd9f'
         'f63cccbbde31c18cf40897fc3a97fc00'
         '259f112a1120451e70be4ff6a5d995c5')

AXIS2C_HOME=/usr/lib/axis2c

prepare(){
  pushd $srcdir/$pkgname-src-$pkgver
    patch -p1 < $srcdir/rampartc-1.3.0-destdir.patch
    patch -p1 < $srcdir/rampartc-1.3.0-quoting.patch
    patch -p1 < $srcdir/rampartc-1.3.0-cast.patch
    patch -p1 < $srcdir/rampartc-1.3.0-segfault.patch
    patch -p1 < $srcdir/rampartc-1.3.0-memleak.patch
    patch -p1 < $srcdir/rampartc-1.3.0-timestamp.patch
    patch -p1 < $srcdir/rampartc-1.3.0-c14n.patch
    patch -p2 < $srcdir/make_paths.patch
  popd
}

build() {
  pushd $srcdir/$pkgname-src-$pkgver
    autoreconf -i
    ./configure --prefix=$AXIS2C_HOME --enable-static=no  --with-axis2=$AXIS2C_HOME/include/axis2-1.6.0 CFLAGS=-w
    make
  popd
}

package() {
  pushd $srcdir/$pkgname-src-$pkgver
    make DESTDIR=$pkgdir install
  popd
}
